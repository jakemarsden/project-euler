import com.jakemarsden.gradle.githooks.GitHooksPlugin

plugins {
  id 'jacoco'
  id 'java-library'
  id 'com.github.sherter.google-java-format' version '0.9'
}

apply plugin: GitHooksPlugin

gitHooks {
  hooks.put 'pre-commit', 'check'
}

repositories {
  jcenter()
}

ext {
  junitVersion = '5.6.2'
}

dependencies {
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
  withSourcesJar()
}

test {
  useJUnitPlatform()
  finalizedBy tasks.jacocoTestReport
}

googleJavaFormat {
  toolVersion = '1.8'
  options style: 'GOOGLE'
}

verifyGoogleJavaFormat {
  mustRunAfter tasks.googleJavaFormat
}

task format {
  dependsOn tasks.googleJavaFormat
}

tasks.withType(JavaCompile) {
  // when formatting code, always do it before any compilation
  // when verifying code format, try do it before compilation to fail fast
  mustRunAfter tasks.googleJavaFormat
  shouldRunAfter tasks.verifyGoogleJavaFormat

  options.compilerArgs += [
      '-parameters',
      '-Werror',
      '-Xlint:all',
      '-Xlint:-processing'
  ]
  options.deprecation = true
  options.encoding = 'UTF-8'
}
